apply from: rootProject.file( 'gradle/java-module.gradle' )

apply plugin: 'application'

if ( project.hasProperty( 'orm4' ) ) {
	apply plugin: 'maven'
	apply plugin: 'hibernate'
}
else {
	apply plugin: 'org.hibernate.orm'

}

apply plugin: "me.champeau.gradle.jmh"

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		mavenLocal()
	}
	dependencies {
		classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.8'

		if ( project.hasProperty( 'orm4' ) ) {
			if ( orm4?.trim() ) {
				classpath "org.hibernate:hibernate-gradle-plugin:${orm4}"
			}
			else {
				classpath "org.hibernate:hibernate-gradle-plugin:4.3.11.Final"
			}
		}
		else if ( project.hasProperty( 'orm5' ) ) {
			if ( orm5?.trim() ) {
				classpath "org.hibernate:hibernate-gradle-plugin:${orm5}"
			}
			else {
				classpath "org.hibernate:hibernate-gradle-plugin:5.4.4-SNAPSHOT"
			}
		}
		else {
			classpath "org.hibernate:hibernate-gradle-plugin:5.4.4-SNAPSHOT"
		}
	}

}

allprojects {
	repositories {
		mavenLocal()
	}
	group 'org.hibernate.benchmarks.orm.hql'
	version '1.0.0-SNAPSHOT'


}

ext {
	hibernateVersion = '5.4.4-SNAPSHOT'
	reportId = ""

	if ( project.hasProperty( 'reportFileNamePrefix' ) ) {
		reportId = reportFileNamePrefix
	}

	if ( project.hasProperty( 'localJar' ) ) {
		hibernateVersion = localJar
	}
	else {
		if ( project.hasProperty( 'orm5' ) ) {
			if ( orm5?.trim() ) {
				hibernateVersion = orm5
			}
		}
		if ( project.hasProperty( 'orm4' ) ) {
			if ( orm4?.trim() ) {
				hibernateVersion = orm4
			}
		}
	}
}

dependencies {
	compile project( ':benchmark-common' )

	compile 'org.openjdk.jmh:jmh-core:1.21'

	compile 'javax.persistence:javax.persistence-api:2.2'
	runtime libraries.h2

	if ( project.hasProperty( 'localJar' ) ) {
		logger.lifecycle( ">>> Using local jar ${rootProject.file( "libs/${localJar}" ).name}" )

		compile files( rootProject.file( "libs/${localJar}" ))
	}
	else {
		compile "org.hibernate:hibernate-core:${hibernateVersion}"
	}
}


if ( !project.hasProperty( 'orm4' ) ) {
	hibernate {
		enhance {
			// any configuration goes here
			enableLazyInitialization = true
		}
	}
}

afterEvaluate {
	dependencies {
		if ( project.hasProperty( 'orm4' ) ) {
			runtime project( ':benchmark-orm4' )
		}
		else {
			runtime project( ':benchmark-orm5' )
		}
	}
}


jmh {
	// for available options, see https://github.com/melix/jmh-gradle-plugin/blob/master/README.adoc#configuration-options
//	exclude = ['StatelessBenchmarkTests']
	benchmarkMode = ['avgt']

	iterations = 5 //default 10
	fork = 5 //default 10
	warmupIterations = 5 // Number of warmup iterations to do.

	def formattedDate = new Date().format( 'yyyy-MM-dd-HHmmss' )

	humanOutputFile = project.file( "reports/jmh/human_${reportId}_${hibernateVersion}_${formattedDate}.txt" )
	resultsFile = project.file( "reports/jmh/results_${reportId}_${hibernateVersion}_${formattedDate}.txt" )
}
